buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
    }
}



curseforge {
    if (project.hasProperty("CURSEFORGE_API")) {
        apiKey = project.CURSEFORGE_API
    } else {
        apiKey = "DUMMY"
    }
    project {
        id = "282433"

        if (project.type == "beta") {
            releaseType = 'beta'
            //changelog = new File("resources/changelog/${project.version}.txt").text
        } else if (project.type == "release") {
            releaseType = 'release'
        } else {
            releaseType = 'alpha'
            changelog = "This version should not be used in your 'productive' world. It contains the latest features, but also the latest bugs.\n Probably not all features are finished.\n To see what might have changed visit our Github page and browse the latest commits."
        }
        addGameVersion project.minecraft_version

        mainArtifact(jar) {
            displayName = "VampirismIntegrations-${project.minecraft_version}-${project.mod_version}"
        }

        relations {
            requiredDependency 'vampirism-become-a-vampire'
            optionalDependency 'biomes-o-plenty'
            optionalDependency 'wthit'
//            optionalDependency 'blood-magic'
//            optionalDependency 'diet'
//            optionalDependency 'survive'
            optionalDependency 'tinkers-construct'
            optionalDependency 'better-animal-models'
            optionalDependency 'betteranimalsplus'
//            optionalDependency 'crafttweaker'
            optionalDependency 'evilcraft'
            optionalDependency 'tough-as-nails'
            optionalDependency 'minecraft-comes-alive-reborn'
        }


    }


}

apply plugin: 'maven-publish'
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {
                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    // Check if the dependency has been mapped with MCP
                    if (dep.version.last().value().last().contains('_mapped_snapshot_') || dep.version.last().value().last().contains('_mapped_stable_') || dep.version.last().value().last().contains('_mapped_official_')) {

                        // Remove the MCP mapped artifact from the dependency list.
                        println 'Surpressing MCP mapped artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                        assert dep.parent().remove(dep)
                    }
                }
            }

            pom {
                description = 'An Minecraft mod that integrating Vampirism with other mods'

                scm {
                    url = 'https://github.com/TeamLapen/VampirismIntegrations'
                    connection = 'scm:git:git://github.com/TeamLapen/VampirismIntegrations.git'
                    developerConnection = 'scm:git:git@github.com:TeamLapen/VampirismIntegrations.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/TeamLapen/VampirismIntegrations/issues'
                }

                developers {
                    developer {
                        id = 'maxanier'
                        name = 'maxanier'
                        roles = ["developer"]
                    }
                }
            }


            // Adds the sources as an artifact.
//            artifact sourcesJar {
//                classifier 'sources'
//            }

            // Adds the deobfuscated jar as an artifact.
//            artifact deobfJar {
//                classifier 'deobf'
//            }
//
//            artifact javadocJar {
//                classifier 'javadoc'
//            }

        }
    }

    repositories {
        maven {
            // Sets the login credentials for your maven.
            credentials {
                username findProperty('maxanier_maven_alias')
                password findProperty('maxanier_maven_token')
            }

            // Sets the maven URL
            url findProperty('maxanier_maven_url')
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

// Make sure it runs after build!
task modrinth (type: TaskModrinthUpload){
    onlyIf {
        project.hasProperty("MODRINTH_API") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
    }
    if (project.type == "beta") {
        versionType = 'BETA'
        //changelog = new File("resources/changelog/${project.version}.txt").text
    } else if (project.type == "release") {
        versionType = 'RELEASE'
    } else {
        versionType = 'ALPHA'
        changelog = "This version should not be used in your 'productive' world. It contains the latest features, but also the latest bugs.\n Probably not all features are finished.\n To see what might have changed visit our Github page and browse the latest commits."
    }
    token = findProperty("MODRINTH_API") ?: "DUMMY"
    projectId = 'ygTFuViO'
    versionName = "VampirismIntegrations-${project.minecraft_version}-${project.mod_version}"
    versionNumber = "${project.minecraft_version}-${project.mod_version}"
    uploadFile = jar
    addGameVersion(project.minecraft_version)
    addLoader('forge')
}